class InstructionPanel{
	field int borderDistance;
	field GameSprite gameSprite;
	field int winStreak;

	constructor InstructionPanel new(){
		let borderDistance = 4;
		let winStreak = 0;
		do drawBorderRectangle(borderDistance, 0, 0, 127, 255);
		let gameSprite = GameSprite.new(5); //five sprites will be used
		do writeInstructions();
		do drawInstructionSprites();
		return this;
	}

	method void dispose() {
		do gameSprite.dispose();
		do Memory.deAlloc(this);
		return;
	}

	method void drawBorderRectangle(int d, int x1, int y1, int x2, int y2){
		do Screen.setColor(true);
		//left borders
		do Screen.drawLine(x1, y1, x1, y2);
		do Screen.drawLine(x1 + d, y1 + d, x1 + d, y2 - d);
		//top borders
		do Screen.drawLine(x1, y1, x2, y1);
		do Screen.drawLine(x1 + d, y1 + d, x2 - d, y1 + d);
		//right borders
		do Screen.drawLine(x2, y1, x2, y2);
		do Screen.drawLine(x2 - d, y1 + d, x2 - d, y2 - d);
		//bottom borders
		do Screen.drawLine(x1, y2, x2, y2);
		do Screen.drawLine(x1 + d, y2 - d, x2 - d, y2 - d);
		return;
	}

	method void drawInstructionSprites(){
		do gameSprite.drawRock(1217);
		do gameSprite.drawPaper(1985);
		do gameSprite.drawScissor(2753);
		do gameSprite.drawLizard(3489);
		do gameSprite.drawSpock(4129);
		return;
	}

	method void writeInstructions(){
		do Output.moveCursor(2, 2);
		do Output.printString("WIN STREAK");
		do Output.moveCursor(2, 13);
		do Output.printInt(winStreak);
		do Output.moveCursor(4, 5);
		do Output.printString("0-ROCK!");
		do Output.moveCursor(6, 5);
		do Output.printString("1-PAPER!");
		do Output.moveCursor(8, 5);
		do Output.printString("2-SCISSOR!");
		do Output.moveCursor(10, 5);
		do Output.printString("3-LIZARD!");
		do Output.moveCursor(12, 5);
		do Output.printString("4-SPOCK!");
		do Output.moveCursor(14, 5);
		do Output.printString("5-QUIT!");
		return;
	}

	method void increaseWinStreak(){
		let winStreak = winStreak + 1;
		do Output.moveCursor(2, 13);
		do Output.printInt(winStreak);
		return;
	}

	method void resetWinStreak(){
		let winStreak = 0;
		do Output.moveCursor(2, 13);
		do Output.printInt(winStreak);
		return;
	}
}